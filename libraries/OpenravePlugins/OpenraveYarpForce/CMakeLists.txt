# Generated by Juan G Victores 2013
# Thanks Rosen Diankov for all OpenRAVE developments

IF (ENABLE_OpenraveYarpForce)

cmake_minimum_required (VERSION 2.6.0)

set(KEYWORD "OpenraveYarpForce")

project(${KEYWORD})

set(CMAKE_PREFIX_PATH ${CMAKE_CURRENT_SOURCE_DIR})
find_package(OpenRAVE REQUIRED)
find_package(YARP REQUIRED)

if( CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX )
  add_definitions("-fno-strict-aliasing -Wall -fPIC")
endif( CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX )

find_package(Boost ${OpenRAVE_Boost_VERSION} EXACT COMPONENTS iostreams python thread)

include_directories(${OpenRAVE_INCLUDE_DIRS} ${YARP_INCLUDE_DIRS} ${CMAKE_CURRENT_SOURCE_DIR} )
if( Boost_INCLUDE_DIRS )
  include_directories(${Boost_INCLUDE_DIRS})
endif()

link_directories(${OpenRAVE_LIBRARY_DIRS} ${Boost_LIBRARY_DIRS})

macro(build_openrave_plugin name)
  add_library(${name} SHARED ${name}.cpp)
  set_target_properties(${name} PROPERTIES COMPILE_FLAGS "${OpenRAVE_CXX_FLAGS}")
  set_target_properties(${name} PROPERTIES LINK_FLAGS "${OpenRAVE_LINK_FLAGS}")
  target_link_libraries(${name} ${YARP_LIBRARIES} ${OpenRAVE_LIBRARIES} )
  # install(TARGETS ${name} DESTINATION lib )
  # install(TARGETS ${name} DESTINATION ${OpenRAVE_LIBRARY_DIRS}/${OpenRAVE_LIBRARIES}-plugins)
  execute_process(COMMAND openrave-config --prefix --plugins-dir
                  OUTPUT_VARIABLE _openrave_config_cmd_output
                  OUTPUT_STRIP_TRAILING_WHITESPACE)
  string(REPLACE "\n" ";" _openrave_config_results ${_openrave_config_cmd_output})
  list(LENGTH _openrave_config_results _len)
  if(_len EQUAL 2)
    list(GET _openrave_config_results 0 _openrave_install_prefix)
    list(GET _openrave_config_results 1 _openrave_plugin_path)
    file(RELATIVE_PATH _relative_path "${_openrave_install_prefix}" "${_openrave_plugin_path}")
    install(TARGETS ${name} DESTINATION ${_relative_path})
  else()
    message(AUTHOR_WARNING "Unsuccessful call to 'openrave-config', the output was: ${_openrave_config_cmd_output}")
  endif()
endmacro(build_openrave_plugin)

build_openrave_plugin(${KEYWORD})

ENDIF (ENABLE_OpenraveYarpForce)

